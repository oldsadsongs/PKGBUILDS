pkgname=elasticsearch-oss
_pkgname=elasticsearch
pkgver=7.8.1
pkgrel=1
pkgdesc="Distributed RESTful search engine built on top of Lucene"
arch=('aarch64')
url="https://www.elastic.co/products/elasticsearch"
license=('Apache')
depends=('java-runtime-headless' 'systemd')
provides=('elasticsearch')
conflicts=('elasticsearch' 'elasticsearch-xpack')
relpkgname=elasticsearch
source=(
  "https://artifacts.elastic.co/downloads/$relpkgname/$pkgname-$pkgver-$arch.rpm"
  elasticsearch-env
  elasticsearch.service
  elasticsearch-sysctl.conf
  elasticsearch-user.conf
  elasticsearch-tmpfile.conf
  elasticsearch.default
)
sha256sums=('1050598b5fc6591febb8adc5bfb9a7792f352375cf042f714a20b7e3de82a849'
            '4f9abef6553c8a1ff29071fa542b5b887254971b3d0252c7196bf38a31157832'
            '841c7487686e7ec22ba8f2243da4b4d652cb5b55331daa3a98054bc983ad0fdb'
            'b3feb1e9c7e7ce6b33cea6c727728ed700332aae942ca475c3bcc1d56b9f113c'
            '815f6a39db6f54bb40750c382ffbdc298d2c4c187ee8ea7e2f855923e2ff354b'
            '0a344d779c550de324bfda385896caf0d728563751c176127b63ebf2354abee0'
            'bb74e5fb8bc28f2125e015395ab05bea117b72bfc6dadbca827694b362ee0bf8')

backup=('etc/elasticsearch/elasticsearch.yml'
        'etc/elasticsearch/log4j2.properties'
        'etc/elasticsearch/jvm.options'
        'etc/default/elasticsearch')

#prepare() {
  #cd "$srcdir"
  #find usr/share/elasticsearch/bin -type f ! -name \*.jar -exec \
  #  sed -r 's;source .*/(.*)-env;source /usr/share/elasticsearch/\1-env;' -i {} +
  #find usr/share/elasticsearch/bin -type f -name "elasticsearch-*" ! -name elasticsearch-bin -exec \
  #  sed 's/`dirname "$0"`/$(dirname "$(realpath "$0")")/' -i {} +
#}

package() {
  cd "$pkgdir"
  install -dm2750 etc/elasticsearch
  install -dm750 etc/elasticsearch/jvm.options.d
  install -dm755 {usr/share,var/lib,var/log}/elasticsearch
  install -dm755 usr/bin

  cd "$srcdir"
  for conf in etc/elasticsearch/*.{yml,options,properties}; do
    install -Dm644 "$conf" "$pkgdir/etc/elasticsearch"
  done
  cp -R usr/share/elasticsearch/{bin,lib,modules,plugins} "$pkgdir"/usr/share/elasticsearch
  cp usr/share/elasticsearch/{*.txt,*.asciidoc} "$pkgdir"/usr/share/elasticsearch

  cd "$pkgdir"/usr/share/elasticsearch
  rm -rf bin/elasticsearch-env

  find bin/ -type f -name elasticsearch-\* ! -name elasticsearch-cli -exec \
    ln -s ../share/elasticsearch/{} "$pkgdir"/usr/{} \;

  ln -s ../../../var/log/elasticsearch logs
  ln -s ../../../var/lib/elasticsearch data

  cd "$pkgdir"
  install -Dm755 "$srcdir"/elasticsearch-env usr/share/elasticsearch/bin/elasticsearch-env
  install -Dm644 "$srcdir"/elasticsearch.service usr/lib/systemd/system/elasticsearch.service
  install -Dm644 "$srcdir"/elasticsearch-user.conf usr/lib/sysusers.d/elasticsearch.conf
  install -Dm644 "$srcdir"/elasticsearch-tmpfile.conf usr/lib/tmpfiles.d/elasticsearch.conf
  install -Dm644 "$srcdir"/elasticsearch-sysctl.conf usr/lib/sysctl.d/elasticsearch.conf
  install -Dm644 "$srcdir"/elasticsearch.default etc/default/elasticsearch
}
